# 2. Terraform

As we discussed in our design, our solution is made up of two Application Components: the Frontend and the Backend. Each has its own codebase of Application Code that needs to be deployed. Unlike previous chapters, where we had Operating System configuration as well, now that we are using Serverless offerings, this is no longer our responsibility as the platform takes care of it for us.

Much of the Terraform setup is very similar to what we have done in previous chapters so we will only focus on new resources needed for our solution. You can check the full source code for this book which is available on GitHub if you want to work with the complete solution.

## Frontend

First we need to provision a Storage Account where we can deploy our Frontend to. The Azure Storage Account is one of the most common Terraform resources to be provisioned, as many other Azure services use Storage Accounts for different purposes. However, we need to configure our Storage Account differently by using an optional block called `static_website`. This block will enable the static website feature and will place the `$web` container in our Storage Account by default.

	resource "azurerm_storage_account" "frontend" {
	  name                     = "st${var.application_name}${var.environment_name}${random_string.main.result}"
	  resource_group_name      = azurerm_resource_group.main.name
	  location                 = azurerm_resource_group.main.location
	  account_tier             = "Standard"
	  account_replication_type = "LRS"
	
	  static_website {
	    index_document     = "index.html"
	    error_404_document = "404.html"
	  }
	}

## Backend

Azure Functions are deployed to a resource called a Function App. They come in two varieties—one for Windows and another for Linux. This can be quite perplexing because isn’t the whole purpose of using a serverless offering so that you don’t have to think about the Operating System? However, the underlying operating system can impact the types of runtimes that are supported for your Azure function. 

In order to provide a function app, we first need to have a service plan. As we mentioned in the previous section, there are multiple types of Service Plans. The two main types are Consumption and Premium. In order to use a Consumption Service Plan, you need to use the SKU name of `Y1`, and in order to use a Premium Service Plan, you need to use either `EP1`, `EP2`, or `EP3`. Each of the Premium Service Plan SKUs has a different set of compute and memory resources.

	resource "azurerm_service_plan" "consumption" {
	  name                = "asp-${var.application_name}-${var.environment_name}-${random_string.main.result}"
	  resource_group_name = azurerm_resource_group.main.name
	  location            = azurerm_resource_group.main.location
	  os_type             = var.function_app_os_type
	  sku_name            = var.function_app_sku
	}

Now that we have a service plan, we can provision one or more function apps for it. The Function Apps do not need to share the same resource group, so you could have a central team manage the Service Plans and have each team manage its own Function Apps that are hosted within the Service Plan.

	resource "azurerm_windows_function_app" "main" {
	  name                       = "func-${var.application_name}-${var.environment_name}-${random_string.main.result}"
	  resource_group_name        = azurerm_resource_group.main.name
	  location                   = azurerm_resource_group.main.location
	  service_plan_id            = azurerm_service_plan.consumption.id
	  storage_account_name       = azurerm_storage_account.function.name
	  storage_account_access_key = azurerm_storage_account.function.primary_access_key
	
	  site_config {
	    application_stack {
	      dotnet_version = "v6.0"
	    }
	    cors {
	      allowed_origins     = ["https://portal.azure.com"]
	      support_credentials = true
	    }
	  }
	
	}

The important thing is that the Operating System of the Service Plan should match the Function App’s Terraform Resource Type. Only `azurerm_windows_function_app` resources should be provisioned to Service Plans with an `os_type` of `Windows`, and likewise only `azurerm_linux_function_app` resources should be provisioned to Service Plans with an `os_type` of `Linux`.

The Function App also needs its own Storage Account provisioned. This should be different than the Storage Account used to provision the front end. While it's a common practice to provision a dedicated Storage Account for the Function App, it's technically possible to use the same Storage Account for both the Function App and the Frontend. However, given that there is no additional cost for an additional Storage Account, you only pay for the storage. I recommend provisioning a dedicated storage account to keep the separation between the two components of your architecture.

## Secrets Management

An important block for a Function App is the `app_settings` block. This is where we can pass secrets to our Azure Functions as well as other parameters that affect our deployment strategy and other runtime configurations.

	  app_settings = {
	    "SCM_DO_BUILD_DURING_DEPLOYMENT" = "false"
	    "WEBSITE_RUN_FROM_PACKAGE"       = "1"
	    "STORAGE_CONNECTION_STRING"      = azurerm_storage_account.function.primary_connection_string
	    "QUEUE_CONNECTION_STRING"        = azurerm_storage_account.function.primary_connection_string
	  }

You can see that we are setting the connection string for the Azure Storage account that we will use to connect to Blob and Queue Storage within the application. We can also use KeyVault to store these secrets using special syntax. 

	@Microsoft.KeyVault(VaultName=kv-fleetops-dev;SecretName=QUEUE_CONNECTION_STRING)

If we replace the previous setting with the new one, we are no longer storing the secret in the Azure Function App. The secret is only in KeyVault. 

	  app_settings = {
	    "QUEUE_CONNECTION_STRING" = "@Microsoft.KeyVault(VaultName=${azurerm_keyvault.main.name};SecretName=${azurerm_keyvault_secret.queue_connection_string.name})"
	  }

This also requires us to setup a role assignment to grant the Azure Function's User Assigned Identity the necessary permissions to access the secrets stored in KeyVault. Without this necessary role assignment, even if we use the special syntax to make a reference to the KeyVault secret correctly, Azure Functions will not be able to access the secrets.

![Resource][image-1]
_Structure of Azure Functions resources_

As you can see, the Azure Function is a much more simple deployment. We don’t need a Virtual Network or any of the other surrounding resources that we provisioned in previous chapters just to get off the ground. For most applications, the built-in security of Azure Functions and KeyVault is sufficient. However, if we wanted to enable private networking because our application has to follow some regulatory compliance, we can do that, but otherwise, it is not required.

[image-1]:	../images/AzureFunction-ResourceStructure.png