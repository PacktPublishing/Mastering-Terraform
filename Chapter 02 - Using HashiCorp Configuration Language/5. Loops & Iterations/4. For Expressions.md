# 4. For Expressions

The `for` expression is a way of iterating within Terraform that does not require you to attach it to a block (i.e., resource, data source, or module). You can use the `for` expression to construct in-memory objects to apply object transformations to streamline block-based iteration or for output. 

## Iterating over a List

When iterating over a `list`, you must specify only one parameter to the `for` expression. This parameter will represent each item within your `list` so that you can access each item within the output block.

	region_names_list = [for s in var.regions : upper("${s.region}${s.country}")]

In the above example, we are iterating over all the objects in `var.regions`. As we do, during each iteration, the current value is accessible in the parameter `s`. We can use the output block to generate any object we desire to be created in the new list that this `for` expression will create.

## Iterating over a Map

When iterating over a map, you must change how you structure your `for` expression. You must specify two instead of one parameter declared immediately after the `for` keyword.

```
	  region_array_from_map = [
	    for k, v in var.regions :
	    {
	      region        = k,
	      address_space = v.address_space
	      node_count    = v.node_count
	    }
	  ]
```

In the above example, you'll see that we specify two parameters for the `for` expression: `k` and `v`. We chose these names as a convention to help us remember what these variables mean within the scope of the `for` expression. `k` represents the map's key, while `v` represents the value. The value can be any type, be it a primitive, collection, or complex object. If we want to access the value object, we access it based on its type. In this example, the value is a complex object with two attributes. In the `for` expressions output block, we specify the structure of the object we want each item in the resulting array to have.

In this case, we are creating an array of objects with three attributes: `region`, `address_space`, and `node_count`. Essentially flattening the original map into an array of objects. The output looks like this:

```
	region_array_from_map = [
	  {
	    "address_space" = "10.0.1.0/24"
	    "node_count" = 5
	    "region" = "eastus"
	  },
	  {
	    "address_space" = "10.0.0.0/24"
	    "node_count" = 8
	    "region" = "westus"
	  },
	]
```

## Outputting a List

The `for` expression will always output either a `list` or an `object`. You can select the output type you want by the character in which you wrap the `for` block. If you wrap the `for` expression in square brackets, then the expression will output a `list`.

```
	region_list = [for s in var.regions : "${s.region}${s.country}"]
```

The above `for` expression will produce the following output:

```
	region_list = [
	  "westus",
	  "eastus",
	]
```

Sometimes, the names of the module or resource outputs don't align precisely with other resources' desired inputs. Therefore, using a `for` expression and outputting a list can help transform these incongruent output values into a format convenient for consumption within another part of your code. 

## Outputting an Object

Wrapping the `for` expression with curly braces will output an `object`.

```
	locals {
	  region_config_object = {
	    for s in var.regions : "${s.region}${s.country}" =>
	    {
	      node_count = s.node_count
	    }
	  }
	}
```

This approach will output an object with attributes for each item in the list of regions in the input variable `regions`. Each attribute will take the name of the concatenation of the region and country names, and its value will be an `object` with a single attribute called `node_count`. The output will look like this:

```
	region_config_object = {
	  "eastus" = {
	    "node_count" = 8
	  }
	  "westus" = {
	    "node_count" = 8
	  }
	}
```

Outputting an object can be very useful in scenarios where you need to generate a JSON or YAML payload. You can reference this payload in another resource or output it so another tool can extract that value from Terraform using the `terraform output` command.

## Converting a List to a Map

One common problem is to convert a `list` into a `map`. This is needed because while a `list` is sometimes the most concise way of storing a simple collection, it cannot be used with the `for_each` iterator. Therefore, if you want to have your cake and eat it too, you need to convert that `list` into a `map`. This can be done with a simple `for` expression that iterates over the list in memory and outputs a map.

```
	locals {
	  foo_list = ["A", "B", "C"]
	
	  foo_map = { 
	    for idx, element in local.foo_list : element => idx
	  }
	}
```

In the above code, we are invoking the `for` expression and outputting an object using curly braces (`{}`). We are taking each element within the list and setting it as the key of our `map` and taking the element’s index within the list and setting it as the value. It’s important to note that this will only work when the items in the `list` are not duplicates.

