# 3. Depends On

Sometimes, when Terraform is planning, it needs help to get the dependency graph right. That's because Terraform can only detect explicit dependencies—but sometimes, you can have implicit dependencies depending on the provider and the resource within that provider. These implicit dependencies occur when a resource requires another, but there is no direct relationship between the resources.

![Resource][image-1]
_Implicit dependency between KeyVault Secrets and KeyVault Access Policy_

A great example is whenever you use Azure KeyVault. You need permissions within the KeyVault itself before creating Secrets. Therefore, by default, the identity running Terraform does not have access to create Secrets within the KeyVault it just created.

```
	resource "azurerm_key_vault" "top_secret" {
	  name     = "kv-top-secret"
	  sku_name = "standard"
	}
	
	resource "azurerm_key_vault_secret" "foo" {
	  name         = "foo"
	  value        = "bar"
	  key_vault_id = azurerm_key_vault.top_secret.id
	}
```

The above code will fail. You need to explicitly grant the identity that Terraform is running under access to the KeyVault to create secrets. I can do this by adding an Access Policy Resource.

```
	data "azurerm_client_config" "current" {}
	
	resource "azurerm_key_vault_access_policy" "terraform" {
	  key_vault_id = azurerm_key_vault.top_secret.id
	  tenant_id    = data.azurerm_client_config.current.tenant_id
	  object_id    = data.azurerm_client_config.current.object_id
	
	  secret_permissions = [
	    "Get", "List", "Set"
	  ]
	}
```

Unfortunately, after we added this Access Policy resource, my code still won't work. When I run `apply,` it will tell in my code! Because there is no explicit dependency between the Secret and the Access Policy, Terraform thinks that both can be created in parallel—thus creating a race condition that will inevitably fail when Terraform attempts to make the Secret.

Therefore, we need to make sure that we define the dependency of the Secret on the Access Policy so that Terraform knows that it needs to create the Access Policy before we attempt to create the Secret.

```
	resource "azurerm_key_vault_secret" "foo" {
	  name         = "foo"
	  value        = "bar"
	  key_vault_id = azurerm_key_vault.top_secret.id
	
	  depends_on = [azurerm_key_vault_access_policy.terraform]
	}
```

By explicitly declaring the dependency between the Secret and the Access Policy, we will solve the problem in our Terraform plan that creates a race condition between them.

[image-1]:	../images/DependsOn-ImplicitDependency.png