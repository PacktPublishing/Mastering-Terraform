# 4. Dynamic Blocks

We know that Terraform represents the objects it provisions as resource blocks, which, as we know, support nested blocks within them. When a nested block supports many instances, it can sometimes be helpful to declare them dynamically using an approach called Dynamic Blocks. With this approach, you essentially drive the number of nested block definitions within a resource block based on a collection of objects.

Let's take the example of our Cosmos DB account that needs to replicate its databases across multiple geo-locations. We can add as many `geo_location` nested blocks as we want and configure them accordingly, but this can get tedious—especially if the configuration for each block is relatively uniform.

```
	resource "azurerm_cosmosdb_account" "db" {
	
	  name                = "cosmos-foobar"
	  location            = azurerm_resource_group.foobar.location
	  resource_group_name = azurerm_resource_group.foobar.name
	  offer_type          = "Standard"
	  kind                = "MongoDB"
	
	  geo_location {
	    location          = "westus"
	    failover_priority = 0
	  }
	
	  geo_location {
	    location          = "eastus"
	    failover_priority = 1
	  }
	
	}
```

We can accomplish the same thing using a Dynamic Block. Suppose we declare a map that defines the regions we want to replicate across and the failover priority of each region. In that case, we can use this `map` to drive a Dynamic Block that sets up our Cosmos DB account correctly—and concisely.

```
	locals {
	  regions = {
	    westus = 0
	    eastus = 1
	  }
	}
```

Now, with the `map` configured with our replication region settings, we can use a dynamic block to instruct the Cosmos DB resource on how to set up its `geo_location` nested blocks.

```
	resource "azurerm_cosmosdb_account" "db" {
	
	  name                = "cosmos-foobar"
	  location            = azurerm_resource_group.foobar.location
	  resource_group_name = azurerm_resource_group.foobar.name
	  offer_type          = "Standard"
	  kind                = "MongoDB"
	
	  dynamic "geo_location" {
	    for_each = local.regions
	    content {
	      location          = geo_location.key
	      failover_priority = geo_location.value
	    }
	  }
	
	}
```

Notice that the `geo_location` becomes a reference to each item in the `local.regions` as we iterate through it, and because `local.regions` is a `map` that means that `geo_location` is an item within this `map`. That means that each `geo_location` is a key/value pair, and we can use `key` and `value` to access the respective values when we set the `location` and `failover_priority` on the `content` of the nested block.
