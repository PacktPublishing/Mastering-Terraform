# 1. Resources

Resources are the most common block that you use when coding in HCL. The Resource Block is what Terraform is all about. You can think of each Resource as a digital twin of something Terraform will provision in the real world.

```
	resource "random_string" "foobar" {
	  length  = 4
	  upper   = false
	  special = false
	}
```

A block’s definition has three parts: the Block Type, the Resource Type, and the Reference Name. In the above example, the Block Type is `resource`, the Resource Type is `random_string`, and the Reference Name is `foobar`. To create dependencies between resources, we use the reference name and type to access output values from the Resource.

```
	resource "azurerm_resource_group" "foobar" {
	  name     = "rg-${random_string.foobar.result}"
	  location = var.location
	}
```

In the above code, we create an Azure Resource Group by referencing the Random String named `foobar`’s output value called `result`. 

Each Resource within a Terraform provider is a tiny semi-independent computer program designed to manage a particular underlying system architecture. These resources define a schema that allows you to control the configuration of those underlying components. Sometimes, this schema is straightforward—while other times, it can be very complex, composed of primitive type attributes and additional custom block definitions nested within the resource block. 

These nested blocks allow you to declare one or more within a resource. The Resource dictates the number of each type of nested block it expects. Sometimes, Resources allow many instances of the same nested block, while other times, they may allow precisely one.

For example, the Azure Cosmos DB service allows you to create hyper-scale NoSQL databases and quickly set up replication across multiple geo-locations. Each geo-location is a nested block within the Cosmos DB resource block.

```
	resource "azurerm_cosmosdb_account" "db" {
	
	  name                = "cosmos-foobar"
	  location            = azurerm_resource_group.foobar.location
	  resource_group_name = azurerm_resource_group.foobar.name
	  offer_type          = "Standard"
	  kind                = "MongoDB"
	
	  consistency_policy {
	    consistency_level = "Eventual"
	  }
	
	  geo_location {
	    location          = "westus"
	    failover_priority = 0
	  }
	
	  geo_location {
	    location          = "eastus"
	    failover_priority = 1
	  }
	
	}
```

As you can see, the block `geo_location` is repeated within the `azurerm_cosmosdb_account` block multiple times. Each instance of the `geo_location` nested block tells this Cosmos DB Account where to replicate the MongoDB databases and the failover priority.