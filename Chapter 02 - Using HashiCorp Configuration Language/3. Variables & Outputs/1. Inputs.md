# 1. Inputs

As we learned in the previous chapter, Terraform operates within the context of a module. That module is scoped physically within a directory. The root module is a special case where its directory is the same as Terraform's Working directory. Whenever you are writing HCL, you are writing within the context of a module, whether the root module or some other module; therefore, you need to consider how to get data into your module and how to get data out of it.

Input variables are an essential design consideration because Terraform modules are simple machines that take inputs and produce outputs.

The input is all about what information the module needs to provision itself, and those inputs can come from wherever. As you design inputs, they should be atomic. 

The module should be able to take the input without any additional manipulation or logic to parse the input variable's value. You should parse the value outside of the module. That doesn't mean that inputs can't be complex objects or collections, but keeping the interface as simple as possible is a good idea. The more complex your inputs are, the more complex your module will be—whether it's your root module or reusable modules that you share across workspaces.

Your root module will likely have the most complex inputs. Consider your input's structure for root modules based on how you inject the parameter into the variable. If you use a variable file written in HCL, using complex types that span multiple lines, whether a list, a map, or a complex object, is effortless. However, suppose you plan to use a Linux Environment Variable or command line argument method to pass in the parameter. In that case, you should reconsider using complex objects as inputs, as they can be challenging to troubleshoot and verify you are getting the correct value into that input.

When you use Linux Environment Variables, it's important to remember they are not very good at storing structured data with complex schema. The most complex schema you see in a Linux environment variable is some delimited text. PATH is an excellent example of this, as it is a delimited text value. You could use a delimited text value as an input variable to simplify injecting the value. The downside is that you'll have to parse the value after you get it into Terraform.

In the case of a root module, this could be desirable because it reduces the complexity of the interface of the two tools, Terraform and other executables. This sort of integration is widespread within automation pipelines where separate pipeline tasks execute different tools, and you pipe the output from one tool into the inputs of the other. Passing values into Terraform from the command line interface is similar to how we pipe the outputs of one module into the inputs of another module. However, this is more seamless within Terraform because HCL can be used to transfer the value. In contrast, with command line tools, you would need additional parsing steps to transform the value into the desired format for Terraform to consume quickly.

```
	variable "foo" {
	  type        = string
	  description = "This is the value for foo. It is needed because 'reasons'. Its value must be less than 6 characters."
	}
```

In the above code, we declare an input variable `foo` with a type of `string` and provide some guidance to the user of our module on how to use this input variable within the `description`.

## Sensitive Data

Sometimes, you may need to input sensitive data such as a password, connection string, or access key. You must annotate your input variables to ensure that Terraform knows not to display them in its output, as this can lead to secret leakage through the operational logs that Terraform emits.

```
	variable "super_secret_password" {
	  type        = string
	  description = "Password that I get from somewhere else"
	  sensitive   = true
	}
```

In the above code, we are annotating our super secret password with the `sensitive` attribute to prevent this secret from being output by Terraform.

## Optional

When building modules that can support many scenarios, you will often need to provide input variables to support the data needs of each scenario. Each supported scenario may only require a subset of the input variables to be specified. In this situation, we should make our input variable optional. 

For primitive types, you can accomplish this quite simply by setting the default value to `null`. 

```
	variable "totally_optional_field" {
	  type        = string
	  description = "Yes, No, or Maybe"
	  default     = null
	}
```

In the above code, we set the default to `null`, allowing the user to ignore this input variable completely.

Setting an input variable to be optional can be more complicated when working with complex objects, as we may want the entire object or its attributes to be optional.

Consider the below code:

```
	variable "person" {
	  type = object({
	    first_name  = string
	    middle_name = string
	    last_name   = string
	  })
	}
```

In the above code, we declare a variable called `person`. Unfortunately, not only is it a non-optional input variable, but every attribute on the `person` object needs to be specified. 

Let's see if we can loosen things up.

```
	variable "person" {
	  type = object({
	    first_name  = string
	    middle_name = optional(string)
	    last_name   = string
	  })
	  default = null
	}
```

In the above code, notice that adding the `default = null` on the `person` variable block allows users of this module to ignore this input variable completely. Additionally, if the user supplies a `person` object, the `middle_name` attribute is not. Now, the supported inputs are much more flexible for the user.

```
person = null
```

The above value sets the entire object to `null`.

```
	person = {
	  first_name = "Keyser"
	  last_name  = "Söze"
	}
```

The above value sets the input variable to an object but omits the `middle_name` attribute.

```
	person = {
	  first_name  = "Keyser"
	  middle_name = ""
	  last_name   = "Söze"
	}
```

Lastly, we set the input variable to an object and specify values for all attributes. Thanks to the inclusion of `default` and `optional` within our input variable declaration, all are equally valid parameter values for our module.

## Validation
When creating modules you will use more broadly in your organization, consider adding some basic validations to your modules' inputs. The `validation` block provides a way to add primitive data validation on incoming input variable values.

Adding validation can significantly reduce apply-time failures that can plague your user if an input value has requirements from the underlying provider that may not be evident through your module's interface. For example, a cloud platform may impose naming conventions on resources such as alpha-numeric only, all lowercase, less than 30 characters, etc. Unless the consumer of your module is aware of the platform-specific constraints, they could encounter difficulty using your module as they attempt to figure out how to get the correct set of values as inputs—especially if your module abstracts the underlying cloud platform's resources in any way. 

```
	variable "name" {
	  type        = string
	  description = "Name of the thing"
	
	  validation {
	    condition     = length(name) < 30
	    error_message = "Length of name must be less than 30"
	  }
	}
```

In the above code, we specify a `condition` that checks the length of the input variable is less than `30`. We could use any valid boolean expression. We are good if it returns either `true` or `false`. We can use any number of functions that Terraform supports. However, we can only reference the variable that the `validation` block lives on—meaning, we can't reference other variables to create complex multi-variable input validation, nor can we make validation conditional on other elements declared within Terraform, be they other variables, resources, data sources, locals or otherwise.
