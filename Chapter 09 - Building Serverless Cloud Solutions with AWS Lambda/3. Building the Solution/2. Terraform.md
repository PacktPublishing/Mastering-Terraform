# Terraform

As we discussed in our design, our solution consists of two Application Components: the front end and the back end. Each has its own codebase of Application Code that needs to be deployed. Unlike previous chapters, where we also had Operating System configuration, now that we are using Serverless offerings, this is no longer our responsibility, as the platform takes care of it for us.

Much of the Terraform setup is very similar to what we have done in previous chapters, so we will only focus on new resources needed for our solution. If you want to work with the complete solution, you can check the full source code for this book, which is available on GitHub.

## Frontend

First, we need to provision an AWS S3 Bucket to which we can deploy our Frontend. The S3 bucket is one of the most common Terraform resources to be provisioned, as many other AWS services use S3 buckets for different purposes. 

	resource "aws_s3_bucket" "frontend" {
	  bucket        = "${var.application_name}-${var.environment_name}-frontend"
	
	  tags = {
	    Name        = "${var.application_name}-${var.environment_name}-frontend"
	    application = var.application_name
	    environment = var.environment_name
	  }
	}

However, we need to configure our S3 bucket a bit differently by using a couple of additional resources. First, we need to configure public access using the `aws_s3_bucket_public_access_block` resource, and then we need to configure our static website using the `aws_s3_bucket_website_configuration` resource. 

	resource "aws_s3_bucket_public_access_block" "frontend" {
	  bucket = aws_s3_bucket.frontend.id
	
	  block_public_acls       = false
	  block_public_policy     = false
	  ignore_public_acls      = false
	  restrict_public_buckets = false
	}

The configuration is pretty simple, but it is critical for enabling our S3 bucket to be accessible over the Internet. By altering our configuration here, we could also opt to host static websites that are not accessible over the Internet. This might be ideal for intranet websites that we only want accessible when on a private network.

	resource "aws_s3_bucket_website_configuration" "frontend" {
	  bucket = aws_s3_bucket.frontend.id
	
	  index_document {
	    suffix = "index.html"
	  }
	
	  error_document {
	    key = "error.html"
	  }
	
	}

This configures the S3 bucket to specify the default web page when it redirects web traffic to the content stored within our bucket. The page `index.html` aligns with what our ASP.NET Blazor web application uses by default.

Lastly, we need to configure Identity & Access Management (IAM) to allow access to our S3 bucket. A common technique within the `aws` provider is to use a Data Source resource to generate IAM Policy documents that can then be attached to other provisioned resources.

	data "aws_iam_policy_document" "frontend" {
	  statement {
	    actions   = ["s3:GetObject"]
	    resources = ["${aws_s3_bucket.frontend.arn}/*"]
	
	    principals {
	      type        = "*"
	      identifiers = ["*"]
	    }
	  }
	}

The above data source emits the correct policy document, which we can use when configuring the S3 Bucket’s policy using an `aws_s3_bucket_policy` resource.

	resource "aws_s3_bucket_policy" "frontend" {
	  bucket = aws_s3_bucket.frontend.id
	  policy = data.aws_iam_policy_document.frontend.json
	
	  depends_on = [aws_s3_bucket_public_access_block.frontend]
	}

## Backend

Lambda Functions are deployed to an `aws_lambda_function` resource, but the most important thing to set up first is the IAM Role you will use for your Lambda Function. This will be how we allow our Lambda Function access to other resources on AWS, such as secrets, logging, and communicating with databases and other services our application code needs to communicate with.

	data "aws_iam_policy_document" "lambda" {
	  statement {
	    effect = "Allow"
	
	    principals {
	      type        = "Service"
	      identifiers = ["lambda.amazonaws.com"]
	    }
	
	    actions = ["sts:AssumeRole"]
	  }
	}

We start with an IAM Policy Document for the `sts:AssumeRole` permissions and scope it to Lambda Functions. Then we define the IAM role and use this as the `assume_role_policy`. 

	resource "aws_iam_role" "lambda" {
	  name               = "${var.application_name}-${var.environment_name}-lambda"
	  assume_role_policy = data.aws_iam_policy_document.lambda.json
	}

We can grant more permissions later by defining additional policies and attaching them to this IAM Role, but more on that later. Now, it’s time to provision our Lambda Function.

	resource "aws_lambda_function" "main" {
	  function_name = "${var.application_name}-${var.environment_name}"
	  role          = aws_iam_role.lambda.arn
	  runtime       = "dotnet6"
	  filename      = "deployment.zip"
	  handler       = "FleetAPI::FleetAPI.Function::FunctionHandler"
	
	  tags = {
	    Name        = "${var.application_name}-${var.environment_name}-lambda"
	    application = var.application_name
	    environment = var.environment_name
	  }
	}

As in the previous two chapters, we consistently tag our AWS resources with `application` and `environment` tags. These tags organize our deployment into an AWS Resource Group for easier centralized management.

A key attribute here is the `runtime`, which in our case is .NET 6, but depending on your technology stack, this will, of course, vary. However, probably the most important attribute is the `handler`. This is also the trickiest one to set, as it needs to be carefully aligned with our application code. The `handler` is a path to a component in our application code. In .NET, this path is made up of three parts: the namespace, the fully qualified class name, and the method name.

We can also use an optional nested block to set additional environment variables to help configure the Lambda Function. 

	environment {
	  variables = {
	    SECRET_SAUCE = random_string.secret_sauce.result
	  }
	}

This can be a useful way to pass in configuration to the Lambda, which is output by other Terraform resources.

## Logging

As we’ve seen, AWS uses IAM Policies to grant access to other foundational services on the platform. This is necessary for even things like logging.

	resource "aws_iam_policy" "lambda_logging" {
	  name        = "${var.application_name}-${var.environment_name}-lambda-logging-policy"
	  description = "Allow Lambda to log to CloudWatch"
	
	  policy = jsonencode({
	    Version = "2012-10-17"
	    Statement = [
	      {
	        Action = [
	          "logs:CreateLogGroup",
	          "logs:CreateLogStream",
	          "logs:PutLogEvents"
	        ]
	        Effect   = "Allow"
	        Resource = "arn:aws:logs:*:*:*"
	      }
	    ]
	  })
	}

Above, we are creating a policy that allows our Lambda Function to write to CloudWatch.

	resource "aws_iam_role_policy_attachment" "lambda_logging" {
	  role       = aws_iam_role.lambda.name
	  policy_arn = aws_iam_policy.lambda_logging.arn
	}

Finally, we must attach this policy to the IAM Role we created for our Lambda Function. 

![Resource][image-1]
_IAM Policy to grant access to CloudWatch logging_

This will allow us to use CloudWatch to see what’s happening inside our application code every time our Lambda Function is executed, which is critical for troubleshooting and debugging.

## Secrets Management

We saw that we could set environment variables on our Lambda Function. Still, if we want better control over our secrets, we may want to use AWS Secrets Manager to manage them and then configure our Lambda Function to access them from there.

For example, we’ll set up a password using the `random_password` resource from the `random` utility provider we reviewed in Chapter 3. Sometimes AWS Services generate secrets on your behalf, and sometimes they allow you to specify your own—a situation where the `random_password` resource can be very useful.

	resource "random_password" "secret_sauce" {
	  length  = 8
	  lower   = false
	  special = false
	}

The above code declares a password that we will use as our secret. Then we need to create a Secrets Manager Secret to hold this secret.

	resource "aws_secretsmanager_secret" "secret_sauce" {
	  name = "secret-sauce"
	
	  tags = {
	    application = var.application_name
	    environment = var.environment_name
	  }
	}

The above code generates the secret, but you must store secret values in the `aws_secretsmanager_secret_version` sub-resource.

	resource "aws_secretsmanager_secret_version" "secret_sauce" {
	  secret_id     = aws_secretsmanager_secret.secret_sauce.id
	  secret_string = random_string.secret_sauce.result
	}

There are additional features that can be enabled to handle automatic rotation and custom encryption that you could also consider.

Now that our secret has been created and stored in Secrets Manager, we must create an IAM policy to grant our Lambda Function access.

	resource "aws_iam_policy" "lambda_secrets" {
	  name        = "${var.application_name}-${var.environment_name}-secrets-policy"
	  description = "Policy to allow Lambda function to access secrets."
	
	  policy = jsonencode({
	    Version = "2012-10-17",
	    Statement = [
	      {
	        Action = ["secretsmanager:GetSecretValue"],
	        Effect = "Allow",
	        Resource = [
	          aws_secretsmanager_secret.secret_sauce.arn
	        ]
	      }
	    ]
	  })
	}

We will use the `aws_iam_role_policy_attachment` to attach the policy to the Lambda Function’s IAM role just as we did for the permissions to log to CloudWatch. If you need to use additional secrets, you can continue to add them to the resource array where `secret_sauce` has been added.

![Resource][image-2]
_IAM Policy to grant access to Secrets Manager secrets_

As you can see, the Lambda Function is a much more simple deployment. We don’t need a Virtual Network or any other surrounding resources we provisioned in previous chapters to get off the ground. For most applications, the built-in security of Lambda Functions and Secrets Manager is sufficient. However, if we wanted to enable private networking because our application has to follow some regulatory compliance, we can do that, but otherwise, it is not required.

[image-1]:	../images/AWSFunction-ResourceStructure-CloudWatch.png
[image-2]:	../images/AWSFunction-ResourceStructure-SecretsManager.png