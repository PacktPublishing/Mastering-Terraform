# 2. Randomizing

The `random` provider offers several ways to add randomness to your Terraform solution. Each `random` resource type may generate different types of random values and have other attributes to control the output. Still, all of them—with only a couple of exceptions—generate the random value through a single output called `result`. They also all have at least one attribute called `keepers`, which triggers Terraform to re-create the Resource. This attribute can be helpful to set when you have transient resources that get replaced often, and you need to ensure there are no name conflicts when destroying and re-creating the Resource.

## 2. Random Strings

Generating random strings can be a great way to guarantee uniqueness across deployments, especially in situations where you are dynamically generating short-lived environments. Depending on the case, there are two ways to generate strings—one for non-sensitive data, such as resource names, and another for sensitive data, such as access keys and passwords.

Generating non-sensitive dynamic names can be done using `random_string`. At the same time, `random_password` can create sensitive values you should protect from leakage by marking them as sensitive if you output them and by securing your State since Terraform will store the resulting value in State.

```
	resource "random_string" "name_suffix" {
	  length           = 6
	  special          = true
	  override_special = "/@£$"
	}
```

The above code generates a random string that can generate unique resource names within projects. Using short random strings to embed within the names of your Resources is a great strategy when working with Resources with minimal name length constraints, as it can be challenging to create a coherent naming convention across all your Resources when one or two of the resources require abnormally small name lengths. This situation is common when resources need to have globally unique names, such as S3 buckets or Azure Storage Accounts. 

```
	locals {
	  resource_name = "foobar${random_string.name_suffix.result}"
	}
```

When you couple a random name suffix with part of your naming convention, you can still have a relatively rational resource name.

```
	resource "random_password" "database" {
	  length           = 16
	  special          = true
	  override_special = "!#$%&*()-_=+[]{}<>:?"
	}
```

### Unique Identifiers

You can also generate a Universally Unique Identifier (UUID), also known as Globally Unique Identifiers (GUID), using the `random_uuid`. This can be helpful when your resource supports very long names, as these non-case-sensitive, alpha-numeric values take the following format: `00000000-0000-0000-0000-000000000000`. You might need this to generate a unique correlation identifier to link resources within your deployments using a common tag.

### Just for Fun

There is also a fun little resource called `random_pet` with a nod to the age-old jest of "Pets vs. Cattle," where you can generate pet names. This Resource is probably not for production but can be helpful in development or lab environments where you could be more creative in resource names. The `random_pet` resource's `id` output will generate names with an adjective-noun format. Here are some sample values I came up with using my sample included in this chapter of this book:

- notable-coyote
- quiet-parakeet
- pure-woodcock
- healthy-monkey
- mint-foal
- pet-serval
- ideal-lab
- special-urchin

You can see that almost all of them don't make much sense, but some can be funny.

## Random Numbers

Generating random numbers can also help generate random names or generate a random index from an array. `random_integer` offers a simple solution that allows you to pick a number in between a specified `min` and `max` value. 

Consider the below array of AWS Availability Zones:

```
	locals {
	  azs = [
	    "us-west-1a", 
	    "us-west-1c",
	    "us-west-1d",
	    "us-west-1e"
	  ]
	}
```

If we wanted to pick a random Availability Zone from this `list`, we could use the `random_integer` to generate a random index from this list.

```
	resource "random_integer" "az" {
	  min = 0
	  max = length(local.azs) - 1
	}
```

The above code would allow us to generate a random integer between `0` and the length of the `list` minus `1` —which would be `4 - 1 = 3`. Therefore, we would generate randomly either `0`, `1`, `2`, or `3`.

We can access the random Availability Zone using the code below:

```
	locals {
	  selected_az = local.azs[random_integer.az.result]
	}
```

Finally, we could use the Availability Zone name to configure our AWS resources:

```
	resource "aws_elb" "foo" {
	
	  availability_zones = [local.selected_az]
	
	  # ... and other aws_elb arguments ...
	}
```

### Beyond Simple Integers

When simple integers aren't enough, you can use `random_id` to generate more sophisticated outputs. The only input is the `byte_length` to control how large the random number generated can be. This resource differs from other `random` provider's resources as it does not have a `result` output but has several others that present the random number in various formats, including decimal, hexadecimal, and Base 64.

```
	resource "random_id" "foo" {
	  byte_length = 8
	}
```

The above code generates a random number with a length of eight bytes. Examples of the output values of the different formats are listed below:

- ID: `IpVgeF7uUY0`
- Decimal: `2492004038924456333`
- Hex: `229560785eee518d`
- Base64 Standard: `IpVgeF7uUY0=`
- Base64 URL: = `IpVgeF7uUY0`

Again, depending on your naming conventions, this can be useful for creating names or tags that uniquely identify your resources.

## Shuffle

Using the previous example, when selecting Availability Zones from a list, we would have to generate various `random_integer` resources if we wanted to select multiple items from that list at random, we would have to generate various `random_integer` resources. Attempting to do so with `random_integer` is already pretty cumbersome, but it becomes more challenging if we have requirements to ensure that the second instance of `random_integer` isn't the same as the first one.

Luckily, an alternative approach to using `random_integer` to select the index of an array would be to use a built-in Resource for the specific task of choosing a random subset of items from a `list`. You can achieve this approach using the `random_shuffle` resource, passing in the `list` and the number of items you want using the `result_count` attribute. The output `result` will be a `list` of strings that you can use. This approach dramatically simplifies our solution if we want our AWS Elastic Load Balancer to span multiple Availability Zones.

Consider the below array of AWS Availability Zones:

```
	locals {
	  azs = [
	    "us-west-1a", 
	    "us-west-1c",
	    "us-west-1d",
	    "us-west-1e"
	  ]
	}
```

We would use the `random_shuffle` to generate two Availability Zones for our ELB.

```
	resource "random_shuffle" "azs" {
	  input        = local.azs
	  result_count = 2
	}
```

Finally, we set the `availability_zones` attribute using the `result` of the `random_shuffle` because its output is the correct type of `list(string)`.

```
	resource "aws_elb" "foo" {
	
	  availability_zones = random_shuffle.azs.result
	
	  # ... and other aws_elb arguments ...
	}
```

This resource is useful, but you need to watch out when using it as it can cause your solution to become non-deterministic—meaning Terraform won’t be able to figure out how to create a plan until the `random_shuffle` resource has been created. This could require you to use targeted `terraform apply` operations to avoid first-time-apply failures.