# 2. Packer

Our solution has a Frontend and a Backend Application component. Although the application code is radically different, the way we build a Virtual Machine Image is not. 

## AWS Plugin

As we discussed in Chapter 4, Packer—like Terraform—is an extensible command line executable. Each cloud platform provides its own plugin for Packer that encapsulates the integration with its services.

	packer {
	  required_plugins {
	    amazon = {
	      source  = "github.com/hashicorp/amazon"
	      version = "~> 1.2.6"
	    }
	  }
	}

Plugins need to be declared within a Packer solution. At the time of writing, the latest version of the AWS Packer Plugin was version 1.2.6. 

The AWS plugin for Packer provides an `amazon-ebs` builder that will generate an AMI by creating a new Virtual Machine from a base image, executing the provisioners, taking an Elastic Block Store (EBS) disk image snapshot and creating an Amazon Machine Image (AMI) from it. This behavior is controlled by the Amazon builder.

	data "amazon-ami" "ubuntu2204" {
	  filters = {
	    architecture        = "x86_64"
	    virtualization-type = "hvm"
	    root-device-type    = "ebs"
	    name                = "ubuntu/images/hvm-ssd/ubuntu-jammy-22.04-amd64-server-*"
	  }
	  owners      = ["099720109477"]
	  most_recent = true
	  region      = var.aws_primary_region
	}

The first input to the Amazon `amazon-ebs` builder is the base image to use when creating the initial Virtual Machine against which the Packer template’s provisioners will be executed. The above code references the latest version of the Ubuntu 22.04 Virtual Machine Image within the target AWS Region.

	source "amazon-ebs" "vm" {
	  region        = var.aws_primary_region
	  ami_name      = "${var.image_name}-${var.image_version}"
	  instance_type = var.aws_instance_type
	  ssh_username  = "ubuntu"
	  ssh_interface = "public_ip"
	  communicator  = "ssh"
	  source_ami    = data.amazon-ami.ubuntu2204.id
	}

The `amazon-ebs` builder references the `amazon-ami` data source in order to ensure that correct base image is used before the provisioners are executed. The `ami_name` is probably the most important attribute on this block as it dictates the version name that the Virtual Machine Image will be referenced by in Terraform `apply` operations.

## Operating System Configuration

In order to avoid access control issues, it’s a good idea to establish the context for the provisioners to be executed within. 

	locals {
	  execute_command = "chmod +x {{ .Path }}; {{ .Vars }} sudo -E sh '{{ .Path }}'"
	}

This is a standard `execute_command` parameter that can be used to set the context for all provisioners. Allowing you to eliminate any unnecessary `sudo` commands within your installation scripts. The above `execution_command` will allow your Packer template scripts to execute as a privileged user.

Our solution is built using ASP.NET Core. Therefore, we need to install .NET 6.0 SDK in order for our solution to work properly on the Virtual Machines. Ubuntu, like other Debian-based distributions of Linux, uses the `apt` command line application to perform package management. By default, Ubuntu includes several public repositories that include most of the common software packages. However, sometimes, you need to set up additional package repositories when the default repositories don’t work. Microsoft hosts its own package repository for `apt`, which houses the correct software package we need to install .NET 6.0 on Ubuntu. Therefore, we need to add that repository before we can use `apt` to install .NET 6.0.

You can see our Packer template includes a file called `dotnet.pref` which has the following contents:

	Package: *
	Pin: origin "packages.microsoft.com"
	Pin-Priority: 1001

We use the Packer `file` provisioner to copy this file to the correct location on the Virtual Machine.

	provisioner "shell" {
	  execute_command = local.execute_command
	  inline = [
	    "cp /tmp/dotnet.pref /etc/apt/preferences.d/dotnet.pref"
	  ]
	}

Then we execute a bash script called `install-dotnet6-prereq.sh` which downloads a `*.deb` file and installs it using the `dpkg` tool. This registers the 3rd-party repository hosted by Microsoft with the Debian package management tool.

Now, we simply run `apt-get update -y` to get the latest version of the packages from all repositories, and we are ready to install .NET 6.0.

	provisioner "shell" {
	  execute_command = local.execute_command
	  inline = [
	    "apt-get install dotnet-sdk-6.0 -y"
	  ]
	}

If we didn’t include the `packages.microsoft.com` repository then this `apt-get install` command would fail with an error message saying that the package named `dotnet-sdk-6.0` could not be found.

## Setup a Service in Linux

Most applications run as a process within Linux that runs perpetually. This is most often the case when the application needs to listen for network traffic—like a web server. Another great benefit of setting up a service in Linux is that the Operating System can auto-start the service every time the Virtual Machine reboots in order to do that you need to setup a service definition file.

	[Unit]
	Description=Fleet Portal
	
	[Service]
	WorkingDirectory=/var/www/fleet-portal
	ExecStart=/usr/bin/dotnet /var/www/fleet-portal/FleetPortal.dll
	
	Restart=always
	RestartSec=10  # Restart service after 10 seconds if the dotnet service crashes
	
	SyslogIdentifier=fleet-portal
	
	User=fleet-portal-svc
	
	Environment=ASPNETCORE_ENVIRONMENT=Production
	Environment=DOTNET_PRINT_TELEMETRY_MESSAGE=false
	
	[Install]
	WantedBy=multi-user.target


This service file needs to be copied to the `/etc/systemd/system` folder, and then, using the `systemctl` command, it will be enabled so that the operating system will automatically start the service when the machine reboots. The `systemctl` command is also useful to `start`, `stop`, and check the `status` of your service.

It’s best practice to run services using their own identity. This allows you to grant the service access to only the resources on the Virtual Machine that it needs.

	  provisioner "shell" {
	    execute_command = local.execute_command
	    inline = [
	      "groupadd fleet-portal-svc",
	      "useradd -g fleet-portal-svc fleet-portal-svc",
	      "mkdir -p /var/www/fleet-portal",
	      "chown -R fleet-portal-svc:fleet-portal-svc /var/www/fleet-portal"
	    ]
	  }

The above code sets up a local User and Group for the service to run under and changes the ownership of the application’s folder at `/var/www/fleet-portal` so that the service’s user account has sufficient access to the application’s executable and supporting files. Both the user and the application’s working directory are specified in the service definition file.

Once the user is ready, we can install the service definition file and enable the service.

	provisioner "shell" {
	  execute_command = local.execute_command
	  inline = [
	    "cp /tmp/fleet-portal.service /etc/systemd/system/fleet-portal.service",
	    "systemctl enable fleet-portal.service"
	  ]
	}

This concludes the Operating System configuration that can be baked into the Virtual Machine Image. Any additional configuration steps require more information from the Cloud Environment that Terraform provisions.
